developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.1.0
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("mit")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
options=('!strip')
install=package.install
sha512sums=('55b26c273ae51d92cf72f0f1f3948191f5f7e5e558b795a3ff8b7afd3e43948012c537c96b20908b0c3e98d05d3aa007a477ed4315e551c984f081a1b387eb5c')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/usr/share/${pkgname}/mattermost/

}
