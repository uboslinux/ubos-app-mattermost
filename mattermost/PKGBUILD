developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.7.1
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("mit")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
depends=(
    'smtp-server'
)
options=('!strip')
install=package.install
sha512sums=('23f56228a48f56a8958adcc64595ba44e4872b70df871a85319df23f094b67bedf14c3e231e2e415859c361a1f4e404e7641e44d6ae6f26db1aadc16e26d32b8')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Open ports
    mkdir -p ${pkgdir}/etc/ubos/open-ports.d
    install -m644 ${startdir}/open-ports.d/mattermost ${pkgdir}/etc/ubos/open-ports.d/

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/usr/share/${pkgname}/mattermost/
    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    install ${startdir}/sql/* ${pkgdir}/usr/share/${pkgname}/sql/

}
