developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=3.2.0
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("mit")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
options=('!strip')
install=package.install
sha512sums=('7d80c0c707f70cd3b886a525b4a118c9cb44f4cd00dde339fbc9211d124677edd5d05157c9dd25b75a07264c531cf8857d52a824004495b12d871e702be3978c')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/usr/share/${pkgname}/mattermost/

}
