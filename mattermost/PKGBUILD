developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=4.6.1
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("custom:${pkgname}")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
releasepage=('https://about.mattermost.com/download/')
depends=(
    'smtp-server'
)
options=('!strip')
install=package.install
sha512sums=('67c8cf3712b8423230fd1217e26eb709db8e1522aac4273fbf7a45d80246c4562aba8fb10ce620489b7ddb1032b661319de85390e230aa4d065a116f020d650d')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

# Scripts
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    cp ${startdir}/bin/* ${pkgdir}/usr/share/${pkgname}/bin

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/usr/share/${pkgname}/mattermost/
    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    install ${startdir}/sql/* ${pkgdir}/usr/share/${pkgname}/sql/

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install ${srcdir}/mattermost/MIT-COMPILED-LICENSE.md -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
