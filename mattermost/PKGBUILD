developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=4.4.2
pkgrel=2
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("mit")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
releasepage=('https://about.mattermost.com/download/')
depends=(
    'smtp-server'
)
options=('!strip')
install=package.install
sha512sums=('d7b7e14fdb1e00db494084568b99630cf5c0b7d038b193879f34b8034eb9894ba4644de23d8d8f3a87d630cf06f98ae9cc5c3a97a9617b74d782fc96d4c3167b')

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Open ports
    mkdir -p ${pkgdir}/etc/ubos/open-ports.d
    install -m644 ${startdir}/open-ports.d/mattermost ${pkgdir}/etc/ubos/open-ports.d/

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

# Scripts
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    cp ${startdir}/bin/* ${pkgdir}/usr/share/${pkgname}/bin

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/usr/share/${pkgname}/mattermost/
    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    install ${startdir}/sql/* ${pkgdir}/usr/share/${pkgname}/sql/

}
