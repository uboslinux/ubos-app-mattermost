developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=5.37.4
# Odd minor version numbers are "quality releases"
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("custom:${pkgname}")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
releasepage=('https://about.mattermost.com/download/')
depends=(
    'smtp-server'
)
options=('!strip')
sha512sums=('6814d0331e176e318ea94a3bab6505f7e4b3d586c7484743f10cc036a1e3dc4e61fcf1a64c70c8a496be1b21ac5d0d85980362842692c73b2df75dcd122470e8')
install=mattermost.install

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Place for instance-specific config file
    install -D -m0755 -d ${pkgdir}/etc/mattermost

# Data directory
    install -D -m0755 -d -p ${pkgdir}/ubos/lib/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/mattermost.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Scripts
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Code
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/ubos/share/${pkgname}/mattermost/

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install ${srcdir}/mattermost/MIT-COMPILED-LICENSE.md -t ${pkgdir}/usr/share/licenses/${pkgname}/

# Log files
    install -D -m0755 -d ${pkgdir}/ubos/log
}
