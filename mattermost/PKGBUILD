developer=http://mattermost.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=5.4.0
pkgrel=1
pkgdesc="An open source, self-hosted Slack-alternative"
arch=('x86_64') # This package downloads a binary, and does not currently recompile it
license=("custom:${pkgname}")
source=("https://releases.mattermost.com/${pkgver}/mattermost-team-${pkgver}-linux-amd64.tar.gz")
releasepage=('https://about.mattermost.com/download/')
depends=(
    'smtp-server'
)
options=('!strip')
sha512sums=('7ceda3739bca7df779bf6ef7aef3c78d7e048fb3160280d056f539b27c40edd4324702ef60fa81910d9293de2befe05e5837160556895c6a2b79d718ae8021f0')
install=mattermost.install

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Place for instance-specific config file
    mkdir -p ${pkgdir}/etc/mattermost

# Data directory
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m0644 ${startdir}/mattermost.conf   -t ${pkgdir}/usr/lib/sysusers.d/

# Scripts
    install -D -m0755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Code
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mattermost
    cp -r -p ${srcdir}/mattermost/* ${pkgdir}/ubos/share/${pkgname}/mattermost/
    install -D -m0644 ${startdir}/sql/* -t ${pkgdir}/ubos/share/${pkgname}/sql/

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install ${srcdir}/mattermost/MIT-COMPILED-LICENSE.md -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
